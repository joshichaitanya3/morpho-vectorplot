/*
Vectorplot module to visualize vector fields on meshes in Morpho

Chaitanya Joshi
chaitanyajoshi.usa@gmail.com

*/
import plot 

/** Visualizes a vector field
 * @param[in] field - the vector field
 * @param[in] grade - Grade to show
 * @param[in] colormap - Colormap to use
 * @param[in] style - style to use
 * @param[in] scale - whether or not to scale values */
fn plotvectorfield(field,
                   dl=1,
                   aspectratio=0.3,
                   noarrows=false,
                   sparsity=nil, 
                   showmesh=true,
                   colormap=nil, 
                   scale=true, 
                   scalebar=nil,
                   colorby=nil,
                   filter=nil, 
                   transmit=nil) {
    
    var mesh = field.mesh()
    var colorfield = Field(mesh, 0.0)
    if (colorby=="norm") {
        colorfield = field.op(fn (x) x.norm())
    }
    else if (colorby=="x") {
        colorfield = field.op(fn (x) x[0])
    }
    else if (colorby=="y") {
        colorfield = field.op(fn (x) x[1])
    }
    else if (colorby=="z") {
        colorfield = field.op(fn (x) x[2])
    }
    else if (iscallable(colorby)) {
        colorfield = field.op(colorby)
    }
    else if (islist(colorby)) {
        if (!iscallable(colorby[0])) Error("VectorPlotArgs","List requires first element to be a callable function").throw()
        var f = colorby[0]
        if (colorby.count()==1) colorfield = field.op(f)
        else  colorfield = field.op(f, colorby[1])
    }
    else if (!isnil(colorby)) {
        Error("InvalidColorby", "The optional `colorby` argument can only be set to `norm`, `x`, `y` or `z`.").throw()
    }

    var densityField = nil
    var maxg = mesh.maxgrade()
    if (!isnil(sparsity) and maxg<1) {
        // We haven't implemented a sparsification scheme if only the vertices are present, so warn about it if a user tries to use it, and don't do anything
        Error("CannotSparsify", "The mesh must have a grade of at least 1 to use the `sparsity` argument for now.").warning()
    }

    else if (!isnil(sparsity)) {
        if (!isnumber(sparsity)) Error("InvalidSparsity", "The optional `sparsity` argument must be a number.").throw()
        if (sparsity<0 || sparsity>1) Error("InvalidSparsityVal", "The optional `sparsity` argument must be between 0 and 1.").throw()

        var elementFunctional = nil
        if (maxg==1) elementFunctional = Length()
        else if (maxg==2) elementFunctional = Area()
        else if (maxg==3) elementFunctional = Volume()
        var vals = elementFunctional.integrand(mesh)
        densityField = Field(mesh, 0, grade=0)
        var avgDensity = vals.sum()/vals.count()
        var conn = mesh.connectivitymatrix(maxg,0)
        for (i in 0...mesh.count()) {
            var sum = 0
            var ids = conn.rowindices(i)
            for (id in ids) {
                sum += vals[0, id]
            }
            densityField[i] = (sum/ids.count() / avgDensity)^(maxg/2) // The maxg/2 is a heuristic to make the density field more uniform. It's not perfect, but it's a start.
            
        }
    }
    var cm=colormap
    if (cm==nil) cm = ViridisMap()    // Default color map

    var bnd=bounds(colorfield)         // Find bounds for the field
    var sc = bnd[1]-bnd[0]
    if (abs(sc)>1e-16) sc=1/sc else sc = 1

    var shape = field.shape()
    var v = mesh.vertexmatrix()
    var nv = mesh.count()
    var out = Graphics()
    for (i in 0...nv) {
        if (!isnil(sparsity)) {
            if (random() > sparsity * densityField[i]) continue
        }
        var x = v.column(i)
        var val = colorfield[i]
        if (scale) val=(val-bnd[0])*sc
        var col = Matrix(cm.rgb(val))
        if (field[i].norm()<1e-12) continue
        if (noarrows) out.display(Cylinder(x-field[i]*dl, x+field[i]*dl, aspectratio=aspectratio, color=col, filter=filter, transmit=transmit))
        else out.display(Arrow(x-field[i]*dl, x+field[i]*dl, aspectratio=aspectratio, color=col, filter=filter, transmit=transmit))
    }
    if (showmesh) out += plotmesh(mesh, grade=1, filter=filter, transmit=transmit)

    if (isobject(scalebar)) {
    var s = scalebar
    s.colormap=cm
    out+=s.draw(bnd[0], bnd[1]) 
    }

    return out 
}
